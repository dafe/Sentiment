plugins {
    id 'com.sourcemuse.mongo' version '1.0.0'
}

group 'gofish'
version '1.0-SNAPSHOT'

dependencies {
    compile "io.vertx:vertx-mongo-client:$vertxVersion"
}

def mainVerticleName = 'com.gofish.sentiment.storage.StorageVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'
if (System.getProperty("os.name").toLowerCase().contains("windows")) {
    doOnChange = '.\\gradlew classes'
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': "$mainVerticleName"
    }
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

mongo {
    logging 'console'
}

// Exclude any generated classes/code from test coverage
jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/gofish/sentiment/storage/rxjava',
                    '**/StorageServiceVertxProxyHandler**',
                    '**/StorageServiceVertxEBProxy**'
            ])
        })
    }
}

jacocoTestITReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/gofish/sentiment/storage/rxjava',
                    '**/StorageServiceVertxProxyHandler**',
                    '**/StorageServiceVertxEBProxy**'
            ])
        })
    }
}

//startMongoDb.dependsOn stopMongoDb
//test.dependsOn stopMongoDb
//testIT.dependsOn startMongoDb
testIT {
    doFirst { startMongoDb }
    doLast { stopMongoDb }
}

run {
    args = [
            'run', "$mainVerticleName",
            "--redeploy=$watchForChange",
            "--launcher-class=$mainClassName",
            "--on-redeploy=$doOnChange",
            '-ha',
            '-conf src/main/resources/vertx-config.json'
    ]
}