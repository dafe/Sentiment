plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'gofish'
version '1.0-SNAPSHOT'

def mainVerticleName = 'com.gofish.sentiment.verticle.SentimentVerticle'
def vertxVersion = '3.3.3'
def junitVersion = '4.12'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

if (System.getProperty("os.name").toLowerCase().contains("windows")) {
    doOnChange = '.\\gradlew classes'
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

mainClassName = 'io.vertx.core.Launcher'

repositories {
    mavenCentral()
}

dependencies {
    compile "io.vertx:vertx-circuit-breaker:$vertxVersion"
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-hazelcast:$vertxVersion"
    compile "io.vertx:vertx-mongo-client:$vertxVersion"
    compile "io.vertx:vertx-rx-java:$vertxVersion"
    compile "io.vertx:vertx-service-discovery:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"

    compile 'io.vertx:vertx-codegen:3.3.3'

    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile "junit:junit:$junitVersion"
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

task annotationProcessing(type: JavaCompile, group: 'build', description: "Generates the Vert.x proxies") {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=${project.projectDir}/src/main"
    ]
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange", '--redeploy-termination-period=2000', '--ha']
}