plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group 'gofish'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

configure (allprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'application'
    apply plugin: 'com.github.johnrengelman.shadow'

    mainClassName = 'io.vertx.core.Launcher'

    ext { vertxVersion = "3.3.3"; junitVersion = "4.12" }

    repositories { mavenLocal(); mavenCentral() }

    dependencies {
        compile ("io.vertx:vertx-core:${vertxVersion}")
        compile ("io.vertx:vertx-hazelcast:${vertxVersion}")
        compile ("io.vertx:vertx-rx-java:${vertxVersion}")
        compile ("io.vertx:vertx-service-discovery:${vertxVersion}")
        compile ("io.vertx:vertx-service-proxy:${vertxVersion}")
        compile ("biz.paluch.logging:logstash-gelf:1.10.0")

        compileOnly ('io.vertx:vertx-codegen:3.3.3')

        testCompile ("io.vertx:vertx-unit:${vertxVersion}") { exclude module: ':job' }
        testCompile ("junit:junit:${junitVersion}")
    }

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/generated'
            }
        }
    }

    task annotationProcessing(type: JavaCompile, group: 'build', description: "Generates the Vert.x proxies") {
        source = sourceSets.main.java
        classpath = configurations.compile + configurations.compileOnly
        destinationDir = project.file('src/main/generated')
        options.compilerArgs = [
                "-proc:only",
                "-processor", "io.vertx.codegen.CodeGenProcessor",
                "-AoutputDirectory=${project.projectDir}/src/main"
        ]
    }

    shadowJar {
        classifier = null
        mergeServiceFiles {
            include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
        }
    }
}